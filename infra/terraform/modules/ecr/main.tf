# Container registry for the API image.

resource "aws_ecr_repository" "this" {
  name = "${var.name}-api"

  # Immutable tags: once `:abc123` is pushed it can never be replaced, so what was scanned
  # and tested is exactly what runs. Deploys reference commit SHAs, never `latest`.
  image_tag_mutability = "IMMUTABLE"

  image_scanning_configuration {
    scan_on_push = true
  }

  encryption_configuration {
    encryption_type = "KMS"
    kms_key         = var.kms_key_arn
  }
}

resource "aws_ecr_lifecycle_policy" "this" {
  repository = aws_ecr_repository.this.name
  policy = jsonencode({
    rules = [{
      rulePriority = 1
      description  = "Keep the most recent ${var.keep_images} images"
      selection = {
        tagStatus   = "any"
        countType   = "imageCountMoreThan"
        countNumber = var.keep_images
      }
      action = { type = "expire" }
    }]
  })
}
